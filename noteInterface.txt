// interface NoteInterfaceConfig
// Use Visual studio code with Jonathan Ouellet Zscript Plugin for Syntaxic coloration
// and code suggession  
// TPQZ 2019 
// Description: creates a noteInterface with one fixed button, 4 parametrized buttons, and a close button
// can be called with :  [RoutineCall,NoteInterfaceConfig,values] 
// the index of the chosed item is stored in gResult

// Declaration of test array
[VarDef,INISIZE,5]
[VarDef,values(INISIZE) ]  
[VarSet,values(0), "Red"]  
[VarSet,values(1), "Susan"]  
[VarSet,values(2), "Ben"] 
[VarSet,values(3), "jhonny"]  
[VarSet,values(4), "franklin"]  
[VarDef,gResult,""] 
[VarDef,result,0]

// [constants section]
// panel
[VarDef,PANELWIDTH,400]
[VarDef,PANELHEIGHT,280]
// buttons
[VarDef,BUTTONHEIGHT,20]
[VarDef,BUTTONWIDTH,320]
[VarDef,BUTTONXPOSITION,40]
[VarDef,BUTTONCOLOR,0x101010]
[VarDef,BUTTONBGCOLOR,0xe4bc72]
// close
[VarDef,CLOSEXPOSITION,40]
[VarDef,CLOSEYPOSITION,250]
[VarDef,CLOSEWIDTH,315]
[VarDef,CLOSECOLOR,0x101010]
[VarDef,CLOSEBGCOLOR,0xffa000]

// [routines section]
[RoutineDef,generatePanel,
       [NoteIButton,/*text*/,/*image*/,/*pressed?*/,1	/*disabled?*/,1,1,PANELWIDTH,PANELHEIGHT,,,0] 
]

[RoutineDef,generateNoteIbutton, // generates the panel buton, first button serve as panel for the other buttons, they will be layed upon it
       [NoteIButton,name,,,,x,y,width,height,bgColor,color,0.7]
,
name,x,y,width,height,bgColor,color]


[RoutineDef,NoteInterfaceConfig,// Note interface routine
    [VarSet, arraySize, [VarSize, array]]
    [VarDef,choosenName, ""] 
    [VarDef, startButton,1] // the Panel button + the fixed button = 2 -- reducee to 1 if fixed button is removed
    [Loop,1000,//Execution loop, will loop untill loop exit
        [VarDef,result,0]
        [RoutineCall,generatePanel] // 
      
        [VarDef,i,0]
        [VarDef,name, ""]
        [VarDef,yValue,0]
        // loop create button list
            [Loop,arraySize,
            
                [VarSet,yValue,(BUTTONHEIGHT*2)*(i+1)] //  vertical offset 
                [VarSet,name, #array(#i)]
                [RoutineCall,generateNoteIbutton,name,BUTTONXPOSITION,yValue,BUTTONWIDTH,BUTTONHEIGHT,BUTTONBGCOLOR,BUTTONCOLOR]
                ,
                i
            ]
        [RoutineCall,generateNoteIbutton,"CLOSE",CLOSEXPOSITION,CLOSEYPOSITION,CLOSEWIDTH,BUTTONHEIGHT,CLOSEBGCOLOR,CLOSECOLOR]
        [VarInc,buttonId]
      
        // loop get result
        [VarSet,result,[Val,[Note,""]]]//get result of the note return an integer
        [VarSet,gResult,(result-(startButton+1))] // start button +current

   
        [If, result>#startButton , // the array buttons 
                [If, result<=[Val,arraySize+startButton]  , // parameter buttons only
                [notebar,[StrMerge, "  " ,[val,arraySize+startButton] ," ",result," " ," - button of array Pressed"]]
                [LoopExit]//exits loop and interface
                , // else 
                 [notebar,[StrMerge,result," " ,"  CLOSE BUTTON ... "]] // close buttons
                [VarSet,gResult,-1] 
                [LoopExit]//exit
                ]
        ]

        [If,result>[Val,arraySize+startButton],//Cancel button 
            [notebar,[StrMerge,result," " ,"  CLOSE BUTTON ... "]]
            [VarSet,gResult,-1] 
            [LoopExit]//exits loop and interface
        ]
    ]//end loop
    ,
    array
]//end routine

// [button section]
[IButton,"test NoteInterface","test NoteInterface",
   [RoutineCall,NoteInterfaceConfig,values]
   // do what you want with gResult
   [If, gResult!=-1,
    [VarSet,choosenName,values(gResult)] 
    [Note, [StrMerge,"You choosed index :",gResult," ",#values(gResult)  ]]
    , // else 
    [Note, "You choosed cancel" ]
   ]
,,100]
